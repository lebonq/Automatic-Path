plugins {
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name + "-mc" + project.minecraft_version
version = project.mod_version 
group = project.maven_group

minecraft {
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases" }
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:5.0.38") { //For config
        exclude(group: "net.fabricmc.fabric-api")
    }
	include "me.shedaniel.cloth:cloth-config-fabric:5.0.38"

	modImplementation "com.terraformersmc:modmenu:2.0.5"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 16
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
			artifact("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar"){
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

curseforge {
  apiKey = "$System.env.APICURSE"
  project {
    id = '460405'
	changelogType = 'markdown'
    changelog = file("changelog.md")
    releaseType = 'release'
    addGameVersion "$project.minecraft_version"
	addGameVersion "Fabric"
	addGameVersion "Java 16"

    mainArtifact("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar") {
      displayName = "Automatic-Path-${project.version}"
	   relations {
        requiredDependency 'fabric-api' // API is required
        embeddedLibrary 'cloth-config' 
		optionalDependency 'modmenu'
	   }
    }
  }
}